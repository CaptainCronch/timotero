extends Node


func _process(_delta: float) -> void:
	if Input.is_action_just_pressed("inventory"):
		#if DisplayServer.window_get_vsync_mode() == DisplayServer.VSYNC_ENABLED:
			#DisplayServer.window_set_vsync_mode(DisplayServer.VSYNC_DISABLED)
			#Engine.max_fps = 0
		#elif DisplayServer.window_get_vsync_mode() == DisplayServer.VSYNC_DISABLED:
			#DisplayServer.window_set_vsync_mode(DisplayServer.VSYNC_ENABLED)
			#Engine.max_fps = 60
		if Input.mouse_mode == Input.MOUSE_MODE_CAPTURED:
			Input.mouse_mode = Input.MOUSE_MODE_VISIBLE
		else:
			Input.mouse_mode = Input.MOUSE_MODE_CAPTURED

	if Input.is_action_just_pressed("escape"):
		get_tree().quit() # temporary for testing

	if Input.is_action_just_pressed("fullscreen"):
		if get_window().mode != Window.MODE_FULLSCREEN:
			get_window().mode = Window.MODE_FULLSCREEN
			
		else:
			get_window().mode = Window.MODE_WINDOWED

# move these helper functions (which should be static!) to their own class? (which will not be an autoload as autoloads can't use static functions) 
func decay_towards(value: float, target: float,
			decay_power: float, delta: float,
			round_threshold : float = 0.0) -> float:

	var new_value := (value - target) * pow(2, -delta * decay_power) + target

	if absf(new_value - target) < round_threshold:
		return target
	else:
		return new_value


func decay_towards_vec2(value: Vector2, target: Vector2,
			decay_power: float, delta: float,
			round_threshold: float = 0.0) -> Vector2:

	var new_value := (value - target) * pow(2, -delta * decay_power) + target

	if (new_value - target).length() < round_threshold:
		return target
	else:
		return new_value


func decay_angle_towards(value: float, target: float,
			decay_power: float, delta: float,
			round_threshold: float = 0.0) -> float:

	var new_value := angle_difference(target, value) * pow(2, -delta * decay_power) + target

	if absf(angle_difference(target, new_value)) < round_threshold:
		return target
	else:
		return new_value


func generate_circle_polygon(radius: float, resolution: int) -> PackedVector2Array:
	var angle_delta: float = (PI * 2) / resolution
	var vector: Vector2 = Vector2(radius, 0)
	var points: PackedVector2Array

	for _i in resolution:
		vector = vector.rotated(angle_delta)
		points.append(vector)

	return points


func ranges_overlap(x1: float, x2: float, y1: float, y2: float) -> bool:
	return maxf(x1, x2) >= minf(y1, y2) and minf(x1, x2) <= maxf(y1, y2)


func vec2_from_xz(vector: Vector3) -> Vector2:
	return Vector2(vector.x, vector.z)


func xz_from_vec2(vector: Vector2, y := 0.0) -> Vector3:
	return Vector3(vector.x, y, vector.y)
